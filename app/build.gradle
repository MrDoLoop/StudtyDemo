import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.application'
apply plugin: 'com.antfortune.freeline'
apply plugin: 'com.neenbedankt.android-apt'


// hugo
apply plugin: 'com.jakewharton.hugo'

// realm
apply plugin: 'realm-android'

// aspectJ=======
apply plugin: 'com.android.application'
repositories {
    mavenCentral()
}

final def log = project.logger
final def variants = project.android.applicationVariants

variants.all { variant ->
    if (!variant.buildType.isDebuggable()) {
        log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
        return;
    }

    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.5",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
        log.debug "ajc args: " + Arrays.toString(args)

        MessageHandler handler = new MessageHandler(true);
        new Main().run(args, handler);
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break;
                case IMessage.WARNING:
                    log.warn message.message, message.thrown
                    break;
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break;
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break;
            }
        }
    }
}
// aspectJ=======

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "studydemo.www.doloop.com.studtydemo"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //freeline集成 http://www.jianshu.com/p/46418dd38218
    freeline {
        hack true
    }

    // debug build 修改verCode
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def flavor = variant.productFlavors[0]
            def flavorName
            if (flavor == null) {
                flavorName = "defaultName"
            } else {
                flavorName = flavor.name
            }
            def outputFile = output.outputFile
            def fileName
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def isRelease = variant.buildType.name.contains('release');
                //variant.name == android.buildTypes.release.name
                if (!isRelease) {
                    // 设置verName
//                    def manifestFile = (File) android.sourceSets.main.manifest.srcFile
//                    def manifestParser = new com.android.builder.core.DefaultManifestParser(manifestFile)
//                    def versionName = manifestParser.getVersionName()
                    variant.mergedFlavor.versionName = versionName + "_dev"

                    // 设置verCode
                    variant.mergedFlavor.versionCode = Integer.MAX_VALUE
                    //manifestParser.getVersionCode()

                    fileName = "${flavorName}-${variant.buildType.name}-v${variant.versionName}(${variant.versionCode}).apk";
                    println fileName
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

}

// 阿里路由
apt {
    arguments {
        moduleName project.getName();
    }
}

dependencies {
    // https://jcenter.bintray.com/ 可以搜索版本
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.1.0'
    // 底栏导航
    compile 'com.ashokvarma.android:bottom-navigation-bar:1.3.1'

    //RxJava
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'io.reactivex:rxandroid:1.2.1'

    // 阿里路由
    apt 'com.alibaba:arouter-compiler:1.0.1'
    compile 'com.alibaba:arouter-api:1.0.3'

    // 另一种路由
    compile 'com.github.campusappcn.AndRouter:router:1.2.4'
    apt 'com.github.campusappcn.AndRouter:compiler:1.2.4'

    // log
    compile 'com.jakewharton.timber:timber:4.5.0'

    // aspectJ
    compile 'org.aspectj:aspectjrt:1.8.10'
}
